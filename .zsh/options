#!/usr/bin/env bash
# shellcheck disable=SC2034
# shellcheck disable=SC2154

# --- General Options --- #

# Use emacs-style key bindings.
setopt emacs; # See: <http://bit.ly/1GwFQUF>

# Extended pattern matching operators.
setopt extended_glob; # See: <http://bit.ly/1Re99Pf>

# Allow comments on the command line when experimenting.
setopt interactive_comments; # See: <http://bit.ly/1Re99Pf>

# Enable the `zmv` command for search/replace in filenames.
autoload -U zmv;

# --- Directory Stack Options --- #

# See: <https://bit.ly/2Ac4dZb>
DIRSTACKSIZE=10           # Max directories in history stack.
setopt auto_cd;           # Converts `arg` to `cd arg` in some cases.
setopt cdable_vars;       # Converts `cd arg` to `cd ~arg` in some cases.
setopt auto_pushd;        # Make `cd` push old directory onto stack.
setopt pushd_ignore_dups; # Don’t push multiple copies of the same directory.

# --- Colors & Syntax Highlighting --- #

# Exports `LS_COLORS`.
eval "$(dircolors ~/.bash/profile/dircolors)";

# shellcheck disable=SC1091
# Syntax highlightingl e.g., `brew install zsh-syntax-highlighting`.
if [[ -f /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  . /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh; fi;

# --- Auto Completion Options --- #

# Initialize auto-complete, see: <https://bit.ly/2n1wH53>
check_zcompdump=0; autoload -Uz compinit;
for _ in ~/.zcompdump*(.Nmh+24); do check_zcompdump=1; break; done;
if [[ "${check_zcompdump}" == 1 ]]; then compinit && compdump; else compinit -C; fi;

# shellcheck disable=SC2206
# Add completions directory to `${fpath}`.
if [[ -f /usr/local/share/zsh-completions ]]; then
  fpath=(/usr/local/share/zsh-completions ${fpath}); fi;

# Load menu functionality for auto-completion.
zmodload -i zsh/complist; # See: <https://bit.ly/2zA6zDX>

# Insert first ambigous entry before opening menu.
setopt menu_complete; # See: <http://bit.ly/1FOec52>

# CaSe-insensitive (all), partial-word, and then substring completion.
zstyle ':completion:*' matcher-list 'M:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*';

# Auto-complete processes; e.g., `kill <TAB>`.
zstyle ':completion:*:*:*:*:processes' command 'ps -u '"${USER}"' -o pid,user,comm -w -w';

# Search for directories in this order when using the `cd` command.
zstyle ':completion:*:cd:*' tag-order 'local-directories directory-stack path-directories';

# Use caching so huge lists are faster.
zstyle ':completion::complete:*' use-cache true;
zstyle ':completion::complete:*' cache-path ~/.cache/zcomp;

# Colors to match-up with directory listings.
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}";

# Select (highlight) items in menu.
zstyle ':completion:*' menu select;

# Style sections.
zstyle ':completion:*' verbose true;
zstyle ':completion:*' group-name '';
zstyle ':completion:*:manuals' separate-sections true;
zstyle ':completion:*:descriptions' format "%F{233}  %df";
zstyle ':completion:*:messages' format "%F{233} %d.%f";
zstyle ':completion:*:warnings' format "%F{233} No matches for %d.%f";

# Expand or complete with dots.
z-expand-or-complete-with-dots() {
  echo -n "\\e[31m...\\e[0m";
  zle expand-or-complete; zle redisplay;
}; zle -N z-expand-or-complete-with-dots;

# Note that TAB in xterm is `Ctrl-I`. See: <http://bit.ly/1FOdg0D>
bindkey '^I' z-expand-or-complete-with-dots;

# Shift-TAB to reverse menu auto-completions.
bindkey '^[[Z' reverse-menu-complete; # See: <http://bit.ly/1cXKgYP>

# --- History-Related Options --- #

# See: <http://bit.ly/1JPkSBF>
SAVEHIST=10000;                   # Max history lines in memory.
HISTSIZE=10000;                   # Max history lines in file.
HISTFILE=~/.zshistory;            # Where to store history lines.
setopt extended_history;          # Write the history file in the ":start:elapsed;command" format.
setopt append_history;            # Parallel sessions will have new entries from their history.
setopt share_history;             # Share history between all sessions.
setopt hist_expire_dups_first;    # Expire duplicate entries first when trimming history.
setopt hist_ignore_dups;          # Don't record an entry that was just recorded again.
setopt hist_ignore_all_dups;      # Delete old recorded entry if new entry is a duplicate.
setopt hist_save_no_dups;         # Don't write duplicate entries in the history file.
setopt hist_find_no_dups;         # Do not display a line previously found.
setopt hist_ignore_space;         # Don't record an entry starting with a space.
setopt hist_reduce_blanks;        # Remove superfluous blanks before recording entry.
setopt hist_verify;               # Don't execute immediately upon history expansion.
setopt hist_beep;                 # Beep when accessing nonexistent history.

# ↑↓ arrows. See: <http://bit.ly/1BTykgS>
bindkey '^[[A' history-beginning-search-backward;
bindkey '^[[B' history-beginning-search-forward;
